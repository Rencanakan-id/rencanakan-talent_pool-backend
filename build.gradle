plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id "org.sonarqube" version "6.0.1.5171"
	id 'jacoco' // For code coverage
	id "au.com.dius.pact" version "4.6.17"
	id "de.undercouch.download" version "5.3.0"
}

group = 'rencanakan.id'
version = '0.0.1-SNAPSHOT'

// Ensure Java 21 is used for compilation
java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring Boot Starters
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	testImplementation 'org.springframework.security:spring-security-test'

	// Validation
	implementation 'jakarta.validation:jakarta.validation-api:3.1.1'
	implementation 'org.hibernate.validator:hibernate-validator:8.0.0.Final'

	// PostgreSQL Driver
	runtimeOnly 'org.postgresql:postgresql'

	// Jackson JSR-310 (Java 8 Date/Time Support)
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.18.3'

	// JWT Libraries
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// Cucumber (BDD Testing)
	testImplementation 'io.cucumber:cucumber-spring:7.21.1'
	testImplementation 'io.cucumber:cucumber-java:7.21.1'
	testImplementation 'io.cucumber:cucumber-junit:7.21.1'

	// Testing Frameworks
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.h2database:h2' // In-memory database for testing
	testImplementation 'org.mockito:mockito-core:5.5.0'
	testImplementation 'org.mockito:mockito-inline:5.2.0'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.assertj:assertj-core:3.24.2'


	// Pact for Contract Testing
	implementation 'au.com.dius.pact:consumer:4.6.17'
	implementation 'au.com.dius.pact:provider:4.6.17'

	//Prometheus
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-registry-prometheus'

}

// Configure Test Task
tasks.named('test') {
	useJUnitPlatform()
}

// JaCoCo Code Coverage Report
tasks.test {
	finalizedBy(tasks.jacocoTestReport) // Generate report after tests run
}
tasks.jacocoTestReport {
	dependsOn(tasks.test) // Ensure tests run before generating the report

	reports {
		xml.required = true
		html.required = true
	}

	// Exclude DTO and configs packages from coverage report
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					'**/dto/**',
					'**/configs/**'
			])
		}))
	}
}

// SonarQube Configuration
sonarqube {
	properties {
		property "sonar.projectKey", "B9JagoNgadpro_gametime_auth"
		property "sonar.organization", "b9jagongadpro"
		property "sonar.java.binaries", "."
		property "sonar.gradle.skipCompile", "true"
	}
}

sonar {
	properties {
		property "sonar.projectKey", "rencanakan-talent_pool-backend"
		property "sonar.projectName", "rencanakan-talent_pool-backend"
	}
}

task downloadNewrelic(type: Download) {
    mkdir 'newrelic'
    src 'https://download.newrelic.com/newrelic/java-agent/newrelic-agent/current/newrelic-java.zip'
    dest file('newrelic')
}

task unzipNewrelic(type: Copy) {
    from zipTree(file('newrelic/newrelic-java.zip'))
    into rootDir
}

task prepareNewRelic(dependsOn: [downloadNewrelic, unzipNewrelic]) {
    doLast {
        def newrelicYml = file("newrelic/newrelic.yml")
        if (newrelicYml.exists()) {
            println "New Relic configuration exists"
        } else {
            println "Creating New Relic configuration"
        }
    }
}

build.dependsOn prepareNewRelic